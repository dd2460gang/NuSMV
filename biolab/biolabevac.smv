---------------------------------------------------------------------------
-- BUTTON                                                                --
---------------------------------------------------------------------------

-- For each door there is a button to request service, that can be
-- pressed. A pressed button stays pressed unless reset by the
-- controller. A button that is not pressed can become pressed
-- nondeterministically.

MODULE Button(reset)
  VAR
    pressed : boolean;
  ASSIGN
    init(pressed) := FALSE;
    next(pressed) :=
      case
        pressed & reset  : FALSE;
        pressed & !reset : TRUE;
        !pressed         : {FALSE,TRUE};
      esac;

  -- REQ: The controller must not reset a button that is not pressed.
  INVARSPEC (reset -> pressed)

---------------------------------------------------------------------------
-- DOOR                                                                  --
---------------------------------------------------------------------------

-- Each airlock (entrance) is equipped with a set of double doors.
-- Each door can receive either open, close or nop commands
-- from the controller, and it responds opening, closing, or
-- preserving the current state.

MODULE Door(door_cmd)
  VAR
    status : { open, closed };

  ASSIGN
    init(status) := closed;
    next(status) :=
      case
        door_cmd = open      : open;
        door_cmd = close     : closed;
        door_cmd = nop       : status;
      esac;

  -- REQ: The controller can issue an open command only if the door is closed.
  INVARSPEC (door_cmd = open  -> status = closed)

  -- REQ: The controller can issue a close command only if the door is open.
  INVARSPEC (door_cmd = close -> status = open)



---------------------------------------------------------------------------
-- AIRLOCK                                                            --
---------------------------------------------------------------------------

-- The airlock has an inner and outer door, and a pair of buttons (on the
-- inside and outside) to request the door to open.
-- For simplicity, the input of both buttons is merged.
-- The airlock also has two states:
--   clean: no hazardous materials in the airlock; prerequisite for the
--   outer door to open
--   dirty: after the inner door has opened
-- After the hazmat suit of a person coming from inside has been disposed
-- of, and the air has been cleaned, a dirty airlock is clean again.
-- Flag "cleaning" indicates if the airlock itself is being cleaned,
-- so a "dirty" airlock can again be clean again in the next state.

MODULE Airlock(inner_door, outer_door, inner_buttons_i, inner_buttons_o, outer_buttons_i, outer_buttons_o)
  VAR
    state: { clean, dirty };
    access: { normal, evac };
    inner_door_cmd : {open, close, nop};
    outer_door_cmd : {open, close, nop};
    reset_inner : boolean;
    reset_outer : boolean;
    cleaning : boolean;

  -- Button is reset only if it is pressed and the door is open.

  ASSIGN
    reset_inner := (inner_buttons & inner_door = open);
    reset_outer := (outer_buttons & outer_door = open);

  ASSIGN
    outer_door_cmd :=
      case
        --reset_outer : nop;
        outer_door = closed & inner_door = closed & !(inner_door_cmd = open) & outer_buttons & !(state = dirty) & access = normal : open;
        outer_door = closed & inner_door = closed & !(inner_door_cmd = open) & outer_buttons_i & !(state = dirty) & access = evac : open;
        outer_door = open & inner_door_cmd = open : close;
        TRUE: nop;
        -- Specify when the door should open or close
  esac;

  ASSIGN
    inner_door_cmd :=
      case
        -- Specify when the door should open or close
        --reset_inner : nop;
        inner_buttons & inner_door = closed & access = normal  : open;
        inner_buttons_i & inner_door = closed & access = evac  : open;
        inner_door = open & outer_buttons & access = normal: close;
        inner_door = open & outer_buttons_i & access = evac: close;

        TRUE: nop;

  esac;

  ASSIGN
    next (state) :=
      case
        inner_door_cmd = open : dirty;
        cleaning & inner_door = closed & outer_door = closed & !(outer_door_cmd = open) & !(inner_door_cmd = open): clean;
        TRUE: state;
        -- Describe when the state becomes "dirty" (or "clean")
  esac;

  ASSIGN
      init(cleaning) := FALSE;
      next(cleaning) :=
           case
               state = clean : FALSE;
               inner_door_cmd = open : FALSE;
               outer_door_cmd = open : FALSE;
               state = dirty & inner_door = closed & outer_door = closed : TRUE;
               TRUE : cleaning;
  esac;

  DEFINE
    inner_buttons := (inner_buttons_i|inner_buttons_o);
    outer_buttons := (outer_buttons_i|outer_buttons_o);

---------------------------------------------------------------------------
-- MAIN                                                                  --
---------------------------------------------------------------------------

-- The main module shows the connection between modules.

MODULE main
  VAR
    inner_door   : Door(airlock.inner_door_cmd);
    outer_door   : Door(airlock.outer_door_cmd);
    inner_buttons_i: Button(airlock.reset_inner);
    inner_buttons_o: Button(airlock.reset_inner);
    outer_buttons_i: Button(airlock.reset_outer);
    outer_buttons_o: Button(airlock.reset_outer);
    --cleaning     : boolean;
    airlock : Airlock(inner_door.status, outer_door.status,
                inner_buttons_i.pressed, inner_buttons_o.pressed, outer_buttons_i.pressed, outer_buttons_o.pressed);

FAIRNESS
  inner_buttons_i.pressed;

FAIRNESS
  outer_buttons_i.pressed;

--FAIRNESS
  --cleaning;

FAIRNESS

      --airlock.outer_door = open;
      !inner_buttons_i.pressed & outer_buttons_i.pressed;
FAIRNESS

    --airlock.outer_door = open;
      inner_buttons_i.pressed & !outer_buttons_i.pressed;

DEFINE
    outer_open := (airlock.outer_door = open);
    inner_open := (airlock.inner_door = open);
    inner_cmd_open := (airlock.inner_door_cmd = open);
    inner_cmd_close := (airlock.inner_door_cmd = close);
    outer_cmd_open := (airlock.outer_door_cmd = open);

-- FAIRNESS
  -- Add another fairness property if needed

---------------------------------------------------------------------------
-- REQUIREMENTS                                                          --
---------------------------------------------------------------------------

  -- The airlock controller must satisfy the following requirements.

  -- REQ: Both doors are never open together
  LTLSPEC
    G (! ((airlock.inner_door = open) & (airlock.outer_door = open)));

  -- REQ: A door only opens if a button is pressed
  LTLSPEC
    G ((airlock.inner_door = open) -> O airlock.inner_buttons);
  LTLSPEC
    G ((airlock.outer_door = open) -> O airlock.outer_buttons);

  -- REQ: If both buttons are pressed, the inner door should take precedence
  LTLSPEC
   G((airlock.inner_buttons & airlock.outer_buttons & !inner_open & !outer_open) -> (inner_cmd_open & !(outer_cmd_open)));
  CTLSPEC
    AF (airlock.inner_buttons);
  CTLSPEC
    AF (airlock.outer_buttons);
  -- REQ: Either door must eventually open (i.e., for either door,
  -- there must eventually occur a state where it is open).
  CTLSPEC
    AF (airlock.inner_door = open);
  CTLSPEC
    AF (airlock.outer_door = open);

  -- REQ: The state of the airlock is always "dirty" if the inner door is open
  CTLSPEC
    AG (airlock.inner_door = open) -> (airlock.state = dirty);
  -- REQ: The next state of the airlock is always "dirty" if the inner door is open
  LTLSPEC
  G ((airlock.inner_door = open) -> X (airlock.state = dirty));
  -- REQ: The outer door never opens for a dirty airlock
  CTLSPEC
  ! EF (airlock.outer_door = open & airlock.state = dirty);
  -- REQ: No button can reach a state where it remains pressed forever.
  CTLSPEC
  ! (EF EG(airlock.inner_buttons));
  CTLSPEC
  ! (EF EG (airlock.outer_buttons));
---------------------------------------------------------------------------
-- Advanced properties for higher grades --
---------------------------------------------------------------------------

  -- REQ: No pressed button can be reset until the door opens.
  LTLSPEC
  G (airlock.inner_buttons -> (!airlock.reset_inner U airlock.inner_door = open));
  LTLSPEC
  G (airlock.outer_buttons -> (!airlock.reset_outer U airlock.outer_door = open));
  --Test opposite
  --!G (airlock.outer_buttons -> (!airlock.reset_outer U airlock.outer_door = open));

  -- REQ: A button must be reset as soon as the door opens
  LTLSPEC
  G (airlock.inner_door_cmd = open -> F(airlock.inner_door = open & airlock.reset_inner));
  LTLSPEC
  G (airlock.outer_door = open -> F(airlock.outer_door = open & airlock.reset_outer));
  --Test opposite
  --!(AG (airlock.inner_door = open -> airlock.reset_inner));

  -- REQ: A dirty airlock remains dirty if one of the doors is open or
  -- "cleaning" is false
  LTLSPEC
  G (airlock.state = dirty -> (airlock.state = dirty U (airlock.inner_door = closed & airlock.outer_door = closed & airlock.cleaning)));

---------------------------------------------------------------------------
-- Automated cleanng --
---------------------------------------------------------------------------
-- REQ: Cleaning only happens (in the next state) if the airlock is dirty
--LTLSPEC
--G (airlock.cleaning -> Y(airlock.state = dirty));
LTLSPEC
G (X (airlock.cleaning) -> (airlock.state = dirty));

-- REQ Cleaning only happens if both doors are closed
CTLSPEC
AG (airlock.cleaning -> (airlock.inner_door = closed & airlock.outer_door = closed));
